buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
    }
}

allprojects {
    group = 'me.dm7.barcodescanner'
    version = '1.8.4'

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/groups/public'
        }
    }

    ext {
        supportLibraryDependency = 'com.android.support:support-v4:23.1.1'
        isLibrary = false
    }
}

subprojects {
    afterEvaluate { Project project ->
        ext.pluginContainer = project.getPlugins()
        if(ext.pluginContainer.hasPlugin("com.android.application") || ext.pluginContainer.hasPlugin("com.android.library")) {
            android {
                compileSdkVersion 23
                buildToolsVersion '23.0.2'

                android {
                    lintOptions {
                        abortOnError false
                    }
                }

                defaultConfig {
                    minSdkVersion 9
                    targetSdkVersion 23
                    versionCode 184
                    versionName "1.8.4"
                }
            }
        }
        if(project.isLibrary && rootProject.hasProperty('sonatypeUsername') && rootProject.hasProperty('sonatypePassword')) {
            configure(project) {
                apply plugin: 'maven'
                apply plugin: 'signing'

                signing {
                    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
                    sign configurations.archives
                }

                uploadArchives {
                    repositories.mavenDeployer {
                        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                        repository(url: sonatypeRepo) {
                            authentication(userName: sonatypeUsername , password: sonatypePassword)
                            releases(updatePolicy: 'always')
                        }

                        snapshotRepository(url: sonatypeSnapshotRepo) {
                            authentication(userName: sonatypeUsername , password: sonatypePassword)
                            snapshots(updatePolicy: 'always')
                        }

                        pom.project {
                            name project.pomName
                            version project.version
                            groupId project.group
                            packaging project.pomPackaging
                            artifactId project.pomArtifactId
                            description project.pomDescription
                            url 'https://github.com/dm77/barcodescanner'

                            scm {
                                url 'scm:git@github.com:dm77/barcodescanner.git'
                                connection 'scm:git@github.com:dm77/barcodescanner.git'
                                developerConnection 'scm:git@github.com:dm77/barcodescanner.git'
                            }

                            licenses {
                                license {
                                    name 'The Apache Software License, Version 2.0'
                                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                    distribution 'repo'
                                }
                            }

                            developers {
                                developer {
                                    id 'dm77'
                                    name 'Dushyanth Maguluru'
                                    email 'dushyanth.maguluru@gmail.com'
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
